plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'io.fabric'
    id 'realm-android'
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.11"
//    id 'com.getkeepsafe.dexcount'
}
apply from: "../dependencies.gradle"
//apply from: '../config/quality/quality.gradle'

repositories {
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

allOpen {
    annotation("com.teo.ttasks.OpenClassOnDebug")
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.teo.ttasks"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName '0.1'
//        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'com.teo.ttasks.DaggerAndroidJUnitRunner'
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file('keystore/release.jks')
            keyAlias 'teo baranga'
            keyPassword "$System.env.TT_KEY"
            storePassword "$System.env.TT_KEY"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "env"
    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            resValue "string", "app_name", "T-Tasks Dev"
            dimension "env"
        }
        prod {
            resValue "string", "app_name", "T-Tasks"
            dimension "env"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk false
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        warningsAsErrors true
        abortOnError true // Fail early.

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    // This allows us see tests execution progress in the output on the CI.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
    dexOptions {
        jumboMode = true
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    def ver = [
        dagger : '2.16',
        espresso : '3.1.1',
        retrofit : '2.5.0',
    ]

    kapt "com.google.dagger:dagger-compiler:${ver.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${ver.dagger}"
    kapt 'dk.ilios:realmfieldnameshelper:1.1.1'

    implementation deps.kotlin.stdlib
    implementation "io.reactivex.rxjava2:rxjava:2.2.5"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "androidx.annotation:annotation:1.0.1"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.preference:preference:1.0.0'
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "com.google.android.gms:play-services-auth:16.0.1"
    implementation "com.google.android.gms:play-services-gcm:16.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "com.google.dagger:dagger:${ver.dagger}"
    implementation "com.google.dagger:dagger-android-support:${ver.dagger}"
    implementation "com.google.firebase:firebase-core:16.0.6"
    implementation "com.google.firebase:firebase-auth:16.1.0"
    implementation "com.google.firebase:firebase-database:16.0.5"
    implementation "androidx.fragment:fragment:1.0.0"
    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-databinding:1.0.0'
    implementation deps.timber
    implementation 'com.androidhuman.rxfirebase2:firebase-auth:16.1.0.0'
    implementation 'com.androidhuman.rxfirebase2:firebase-auth-kotlin:16.1.0.0'
    implementation 'com.androidhuman.rxfirebase2:firebase-database:16.0.3.0'
    implementation 'com.androidhuman.rxfirebase2:firebase-database-kotlin:16.0.3.0'
    implementation 'com.androidhuman.rxfirebase2:firebase-core:16.0.5.0'
    implementation "com.squareup.retrofit2:retrofit:${ver.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${ver.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${ver.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.1"
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
    implementation 'com.mikepenz:aboutlibraries:6.2.1'
    implementation 'com.mikepenz:materialdrawer:6.1.2'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.3.original@aar'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.artemzin.rxjava:proguard-rules:1.3.3.0'
    implementation 'com.github.strooooke:appbarsyncedfab:v0.4'
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.8"
    compileOnly 'javax.annotation:jsr250-api:1.0'

//    debugImplementation libraries.stetho
//    debugImplementation 'com.uphyca:stetho_realm:2.1.0'

    // Dependencies for local unit tests
    testImplementation libraries.junit
    testImplementation deps.kotlin.jUnit
    testImplementation "io.mockk:mockk:1.9"

    androidTestImplementation "androidx.recyclerview:recyclerview:1.0.0"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    // Espresso UI Testing dependencies.
    androidTestImplementation "androidx.test.espresso:espresso-core:${ver.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${ver.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${ver.espresso}"
    androidTestImplementation "io.mockk:mockk-android:1.9"
}

kapt {
    javacOptions {
        // Increase the max count of errors from annotation processors.
        // Default is 100.
        option("-Xmaxerrs", 500)
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500" // or whatever number you want
    }
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:exifinterface:${ver.support}"
    }
}

// This needs to be at the bottom so that no dependency collisions are introduced
apply plugin: 'com.google.gms.google-services'
