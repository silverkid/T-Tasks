// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        /** Project-wide versions */
        VER = [
                kotlin: '1.3.61'
        ]
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public'}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${VER.kotlin}"
//        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'io.realm:realm-gradle-plugin:6.1.0'
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public'}
        maven { url 'https://jitpack.io' }
    }
    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
        tasks.withType(Test) {
            maxParallelForks = Runtime.runtime.availableProcessors()
        }
    }
}
